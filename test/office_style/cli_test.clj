(ns office-style.cli-test
  (:require [clojure.edn :as edn]
            [clojure.test :refer [deftest is]]
            [office-style.cli :as cli])
  (:import [java.io ByteArrayOutputStream]
           [java.util.zip ZipEntry ZipOutputStream]))

(defn zip-bytes [entries]
  (let [out (ByteArrayOutputStream.)]
    (with-open [zip (ZipOutputStream. out)]
      (doseq [[path text] entries]
        (.putNextEntry zip (ZipEntry. path))
        (.write zip (.getBytes text "UTF-8"))
        (.closeEntry zip)))
    (.toByteArray out)))

(def sample-bytes
  (zip-bytes
   {"ppt/presentation.xml" "<p:presentation><p:sldSz cx=\"9144000\" cy=\"5143500\" type=\"wide\"/></p:presentation>"
    "ppt/theme/theme1.xml" "<a:theme><a:clrScheme><a:accent1><a:srgbClr val=\"112233\"/></a:accent1></a:clrScheme><a:fontScheme><a:majorFont><a:latin typeface=\"Aptos Display\"/></a:majorFont></a:fontScheme></a:theme>"
    "ppt/slides/slide1.xml" "<p:sld/>"
    "ppt/slideLayouts/slideLayout1.xml" "<p:sldLayout/>"
    "ppt/slideMasters/slideMaster1.xml" "<p:sldMaster/>"}))

(deftest usage-lists-commands
  (let [usage-fn (ns-resolve 'office-style.cli 'usage)
        usage (@usage-fn)]
    (is (re-find #"extract" usage))
    (is (re-find #"template" usage))
    (is (re-find #"preview" usage))
    (is (re-find #"svgraph" usage))))

(deftest package-bin-points-to-cljs-wrapper
  (let [package-json (slurp "package.json")
        bin (java.io.File. "bin/kotoba-office-style.cljs")]
    (is (re-find #"\"kotoba-office-style\"\s*:\s*\"bin/kotoba-office-style\.cljs\"" package-json))
    (is (not (re-find #"bin/kotoba-office-style\.js" package-json)))
    (is (.exists bin))
    (is (.canExecute bin))))

(deftest require-file-validates-required-path
  (let [require-file-fn (ns-resolve 'office-style.cli 'require-file)]
    (is (= "deck.pptx" (@require-file-fn "deck.pptx")))
    (is (thrown-with-msg? clojure.lang.ExceptionInfo
                          #"office-style cli"
                          (@require-file-fn nil)))))

(deftest extract-command-prints-style-ir
  (let [printed (with-out-str
                  (with-redefs [office-style.cli/read-bytes (fn [path]
                                                   (is (= "deck.pptx" path))
                                                   sample-bytes)]
                    (cli/-main "extract" "deck.pptx")))
        ir (edn/read-string printed)]
    (is (= :style-ir (:office-style/kind ir)))
    (is (= "112233" (get-in ir [:office-style/colors :office-style.color/accent1])))
    (is (= ["ppt/slides/slide1.xml"] (:office-style/slides ir)))))

(deftest template-command-writes-template-edn
  (let [out (java.io.File/createTempFile "office-style-template" ".edn")]
    (try
      (with-redefs [office-style.cli/read-bytes (fn [_] sample-bytes)]
        (cli/-main "template" "deck.pptx" (.getAbsolutePath out)))
      (let [template (edn/read-string (slurp out))]
        (is (= 1 (:office-style/template-version template)))
        (is (= "112233" (get-in template [:office-style/colors :office-style.color/accent1])))
        (is (= ["ppt/slideLayouts/slideLayout1.xml"] (:office-style/layouts template))))
      (finally
        (.delete out)))))

(deftest preview-command-writes-svg
  (let [out (java.io.File/createTempFile "office-style-preview" ".svg")]
    (try
      (let [printed (with-out-str
                      (with-redefs [office-style.cli/read-bytes (fn [_] sample-bytes)]
                        (cli/-main "preview" "deck.pptx" (.getAbsolutePath out))))
            summary (edn/read-string printed)
            svg (slurp out)]
        (is (= (.getAbsolutePath out) (:office-style/path summary)))
        (is (re-find #"<svg" svg))
        (is (re-find #"StyleIR preview" svg)))
      (finally
        (.delete out)))))

(deftest svgraph-command-prints-presentation-edn
  (let [printed (with-out-str
                  (with-redefs [office-style.cli/read-bytes (fn [_] sample-bytes)]
                    (cli/-main "svgraph" "deck.pptx")))
        graph (edn/read-string printed)]
    (is (= "svgraph-presentation/1" (:svgraph/version graph)))
    (is (= [9144000 5143500] (:svgraph/slide-size graph)))
    (is (= ["ppt/slides/slide1.xml"]
           (map :svgraph/part (:svgraph/slides graph))))))

(deftest svgraph-command-writes-presentation-edn
  (let [out (java.io.File/createTempFile "office-style-svgraph" ".edn")]
    (try
      (with-redefs [office-style.cli/read-bytes (fn [_] sample-bytes)]
        (cli/-main "svgraph" "deck.pptx" (.getAbsolutePath out)))
      (let [graph (edn/read-string (slurp out))]
        (is (= "svgraph-presentation/1" (:svgraph/version graph)))
        (is (= ["ppt/slides/slide1.xml"]
               (map :svgraph/part (:svgraph/slides graph)))))
      (finally
        (.delete out)))))
